plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.12'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.asciidoctor.jvm.convert' version '3.3.2' // gradle 7 버전 이후에 적용해야 하는 plugin
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    //asciidoctorExtensions // dependencies 에서 적용한 것 추가
}

repositories {
    mavenCentral()
}

/******* Start Spring Rest Docs *******/
ext {
    snippetsDir = file('./build/generated-snippets') // 변수 선언
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir // // 위에서 작성한 snippetsDir 디렉토리를 test의 output으로 구성하는 설정 -> 스니펫 조각들이 build/generated-snippets로 출력
}

asciidoctor.doFirst {
    delete file("src/main/resources/static/docs") // asciidoctor가 실행될 때 static/docs 폴더 비우기
}

asciidoctor {
    inputs.dir snippetsDir // snippetsDir 를 입력으로 구성
    //configurations 'asciidoctorExtensions' // 위에서 작성한 configuration 적용
    dependsOn test // test 작업 이후에 작동하도록 하는 설정

//    // source가 없으면 .adoc파일을 전부 html로 만들어버림
//    // source 지정시 특정 adoc만 HTML로 만든다.
//    sources{
//        include("**/index.adoc","**/common/*.adoc")
//    }
//
//    // 특정 .adoc에 다른 adoc 파일을 가져와서(include) 사용하고 싶을 경우 경로를 baseDir로 맞춰주는 설정입니다.
//    // 개별 adoc으로 운영한다면 필요 없는 옵션입니다.
//    baseDirFollowsSourceFile()
}

// asccidoctor 작업 이후 생성된 HTML 파일을 static/docs 로 copy
task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")

}
// build의 bootJar 시 asciidoctor에 의존하여 /static/docs에 index.html이 생성
bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

// build 시 codyDocument 실행
build {
    dependsOn copyDocument
}
/******* End Spring Rest Docs *******/

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc' // MockMvc를 restdocs에 사용할 수 있게 해주는 라이브러리 입니다.
    //asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor' // build/generated-snippets 에 생긴 .adoc 조각들을 프로젝트 내의 .adoc 파일에서 읽어들일 수 있도록 연동
}
